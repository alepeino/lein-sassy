(ns leiningen.sass
  (:require [robert.hooke :as hooke]
            [leiningen.lein-sassy.renderer :refer :all]
            [leiningen.lein-sassy.options :refer :all]
            [leiningen.lein-sassy.file-utils :refer :all]
            [leiningen.help :as lhelp]
            [leiningen.clean :as lclean]
            [leiningen.compile :as lcompile]
            [leiningen.core.main :as lmain]
            [cemerick.pomegranate :only [add-dependencies]]))

(defn- once
  "Compile files once."
  [container runtime options]
  (render-all! container runtime options))

(defn- compile-hook [task & args]
  (apply task args)
  (when-let [options (get-sass-options (first args))]
    (let [{container :container runtime :runtime} (init-renderer options)]
      (once container runtime options))))

(defn- watch
  "Automatically recompile when files are modified."
  [container runtime options]
  (render-all! container runtime options)
  (watch-and-render! container runtime options))

(defn- clean
  [options]
  (println "Deleting files generated by lein-sass in " (:dst options))
  (clean-all! options))

(defn- clean-hook [task & args]
  (apply task args)
  (when-let [options (get-sass-options (first args))]
    (clean options)))

(defn sass
  {:help-arglists '[[once] [watch]]
   :subtasks [#'once #'watch]
   :doc "Compile Sass files."}

  ([project]
    ((resolve 'leiningen.core.main/abort) (lhelp/help-for "sass")))

  ([project subtask & args]
    (if-let [options (get-sass-options project)]
      (let [{container :container runtime :runtime} (init-renderer options)]
        (case subtask
          "once" (once container runtime options)
          (or "auto" "watch") (watch container runtime options)
          "clean" (clean options)
          (lmain/warn subtask " not found.")))
      ((resolve 'leiningen.core.main/abort) "Invalid options in project.clj."))))

(defn activate []
  (hooke/add-hook #'lclean/clean #'clean-hook)
  (hooke/add-hook #'lcompile/compile #'compile-hook))
